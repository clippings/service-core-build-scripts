apply plugin: 'org.openapi.generator'

dependencies {
    implementation "org.openapitools:jackson-databind-nullable:${ext.properties['jackson_databind_nullable.version'] ?: '0.2.3'}"
}

if (project.hasProperty('serverOpenApiFile')) {
    final OPENAPI_FILE = project.ext['serverOpenApiFile'];
    final API_ROOT = "${buildDir}/generated/api"

    // generate APIs (interfaces and models only) using the provided openapi.yaml
    openApiGenerate {
        generatorName = "spring"
        skipValidateSpec = false
        inputSpec = OPENAPI_FILE
        outputDir = API_ROOT
        apiPackage = "com.dt.${project.name}.api"
        modelPackage = "com.dt.${project.name}.dto"
        configOptions = [
                basePackage                : "com.dt.${project.name}",
                interfaceOnly              : "true",
                skipDefaultInterface       : "true",
                // hateoas: "true",
                performBeanValidation      : "true",
                prependFormOrBodyParameters: "true",
                generateSupportingFiles    : "false",
                legacyDiscriminatorBehavior: "false",
                // Whether to use tags for creating interface and controller class names
                // Will split the API interfaces per tag name
                useTags                    : "true",
                // Don't add the OpenAPI/Swagger annotations. We don't need to provide
                //more documentation. The OpenAPI specification is the source of truth.
                //Select the complementary documentation annotation library.
                annotationLibrary          : "none",
                //No OpenAPI documentation provider we already have the documentation
                documentationProvider      : "none",
                delegatePattern            : "false",
                apis                       : "",
                models                     : ""
        ]
    }

    sourceSets.main.java.srcDir files("${buildDir}/generated/api/src/main/java").builtBy('openApiGenerate')
    compileJava.dependsOn tasks.openApiGenerate
}
