apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

final MAPSTRUCT_VERSION = findProperty('map-struct.version') ?: '1.5.3.Final'

dependencies {
    // spring dependencies, use the starters - they guarantee compatibility
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    //===start===
    //  Spring Cloud Sleuth for distributed tracing with Open Telemetry support
    implementation('org.springframework.cloud:spring-cloud-starter-sleuth') {
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-sleuth-brave'
    } //  We want to exclude the default tracer coming from Sleuth...
    // ... and we want to include the one coming from Spring Cloud Sleuth OTel
    implementation 'org.springframework.cloud:spring-cloud-sleuth-otel-autoconfigure'
    // finally - adding opentelemntry exporter
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
    implementation 'io.opentelemetry:opentelemetry-extension-trace-propagators'
    //===end===
    // prometheus metrics
    implementation("io.micrometer:micrometer-registry-prometheus")
    // json-formatted log file
    implementation "net.logstash.logback:logstash-logback-encoder:${findProperty('logstash-logback-encoder.version') ?: '7.2'}"
    // support for json-patch is optional
    implementation "com.github.java-json-tools:json-patch:${findProperty('json-patch.version') ?: '1.13'}"
    // support object mapping with map-struct
    implementation "org.mapstruct:mapstruct:${MAPSTRUCT_VERSION}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${MAPSTRUCT_VERSION}"
    // dev
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    // testing
    implementation platform("org.testcontainers:testcontainers-bom:${findProperty('testcontainers.version') ?: '1.17.3'}")
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:junit-jupiter"
}

// spring cloud bom
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${findProperty('spring-cloud.version') ?: '2021.0.4'}"
        mavenBom "org.springframework.cloud:spring-cloud-sleuth-otel-dependencies:${findProperty('spring-cloud-sleuth-otel.version') ?: '1.1.0'}"

    }
}

// by default start with dev profile, that enables local DB and text-based logging
bootRun {
    args = ['--spring.profiles.active=dev']
}

// generate META-INF/build-info.properties used by Actuator
springBoot {
    buildInfo()
}

test {
    onlyIf { !project.hasProperty('skipTest') }
    useJUnitPlatform()
}
