apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    // spring dependencies, use the starters - they guarantee compatibility
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // we enable verbose logging of SQL queries, that includes passed parameters
    implementation "com.github.gavlyukovskiy:datasource-proxy-spring-boot-starter:${ext.properties['datasource_proxy.version'] ?: '1.8.0'}"

    // swagger ui
    implementation "org.webjars:swagger-ui:${ext.properties['swagger_ui.version'] ?: '4.11.1'}"

    // DB driver and the DB migration tools
    implementation "org.postgresql:postgresql:${ext.properties['postgresql.version'] ?: '42.5.0'}"
    implementation "org.liquibase:liquibase-core:${ext.properties['liquibase.version'] ?: '4.17.0'}"
    // JSON-formatter for the log files
    implementation "net.logstash.logback:logstash-logback-encoder:${ext.properties['logstash_logback_encoder.version'] ?: '7.2'}"
    implementation "com.github.java-json-tools:json-patch:${ext.properties['json_patch.version'] ?: '1.13'}"

    // dev
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // spring test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // test containers are used during testing
    implementation platform("org.testcontainers:testcontainers-bom:${ext.properties['testcontainers.version'] ?: '1.17.3'}")
    testImplementation "org.testcontainers:postgresql"
    testImplementation "org.testcontainers:junit-jupiter"
}

// by default start with dev profile, that enables local DB and text-based logging
bootRun {
    args = ['--spring.profiles.active=dev']
}

// generate META-INF/build-info.properties used by Actuator
springBoot {
    buildInfo()
}

test {
    onlyIf { !project.hasProperty('skipTest') }
    useJUnitPlatform()
}
